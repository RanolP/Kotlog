plugins {
    id 'org.jetbrains.dokka' version '0.9.16'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

apply plugin: 'kotlin-platform-jvm'

repositories {
    mavenCentral()
}

dependencies {
    expectedBy project(':common')

    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version

    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test-junit', version: kotlin_version
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-test', version: kotlin_version
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

sourceCompatibility = '1.8'

jar {
    project(':common').configurations.archives.allArtifacts.each {
        from zipTree(it.file.absolutePath)
    }
}

shadowJar {
    classifier = null
    version = null
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Assembles sources JAR'
    classifier = 'sources'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    linkMapping {
        dir = 'src/main/kotlin'
        url = 'https://github.com/RanolP/mfsjea.jvm/blob/master/src/main/kotlin'
        suffix = '#L'
    }
}

task dokkaJar(type: Jar) {
    from dokka {
        moduleName = 'kotlog'
    }
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = 'Assembles Kotlin docs with Dokka'
    classifier = 'javadoc'
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
    archives dokkaJar
}
